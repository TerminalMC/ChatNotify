plugins {
    id("multiloader-loader")
    id("net.neoforged.moddev")
}

dependencies {
    // JiJ MixinExtras
    implementation(jarJar("io.github.llamalad7:mixinextras-neoforge:${mixinextras_version}"))

    // CommandKeys
    implementation("maven.modrinth:65UyswbY:${commandkeys_version}")

    // ChatHeads
    implementation("maven.modrinth:Wb5oqrBJ:${chatheads_version}")
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(":common").file("src/main/resources/META-INF/accesstransformer.cfg")
    if (at.exists()) accessTransformers.from(at.absolutePath)
    parchment {
        minecraftVersion = parchment_minecraft_version
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty("neoforge.enabledGameTestNamespaces", "${mod_id}")
            // Unify the run config names with fabric
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})"
        }
        client {
            client()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir "src/generated/resources" }
